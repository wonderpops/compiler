CompilationUnit     -->     ProgramModule
ProgramModule       -->     yprogram yident ProgramParameters ';' Block '.'
IdentList           -->     yident {',' yident}


Block               -->     [Declarations] StatementSequence
Declarations        -->     [ConstantDefBlock]
                            [VariableDeclBlock]
                            [SubprogDeclList]
ConstantDefBlock    -->     yconst ConstantDef ';' {ConstantDef ';'}
VariableDeclBlock   -->     yvar VariableDecl ';' {VariableDecl ';'}
ConstantDef         -->     yident '=' ConstExpression
VariableDecl        -->     IdentList ':' Type


ConstExpression     -->     [UnaryOperator] ConstFactor
                          | " ' " ycharacter " ' "
                          | ynil
ConstFactor         -->     yident
                          | ynumber
                          | ynil
Type                -->     yident
                          | ArrayType
ArrayType           -->     yarray '[' Subrange {',' Subrange} ']' yof Type
Subrange            -->     ConstFactor '..' ConstFactor
                          | " ' " ycharacter '..' ycharacter " ' "


StatementSequence   -->     ybegin Statement {';' Statement} yend
Statement           -->     Assignment
                          | ProcedureCall
                          | IfStatement
                          | WhileStatement
                          | RepeatStatement
                          | ForStatement
                          | IOStatement
                          | MemoryStatement
                          | StatementSequence
                          | empty
Assignment          -->     Designator ':=' Expression
ProcedureCall       -->     yident [ActualParameters]
IfStatement         -->     yif Expression ythen Statement
                             [yelse Statement]
WhileStatement      -->     ywhile Expression ydo Statement
RepeatStatement     -->     yrepeat StatementSequence yuntil Expression
ForStatement        -->     yfor yident ':=' Expression WhichWay Expression
                             ydo Statement
WhichWay            -->     yto | ydownto
IOStatement         -->     yread '(' DesignatorList ')'
                          | yreadln [ '(' DesignatorList ')' ]
                          | ywrite '(' ExpList ')'
                          | ywriteln [ '(' ExpList ')' ]
DesignatorList      -->     Designator {',' Designator }
Designator          -->     yident [ DesignatorStuff ]
DesignatorStuff     -->     {'.' yident | '[' ExpList ']' | '^' }
ActualParameters    -->     '(' ExpList ')'
ExpList             -->     Expression { ',' Expression }

Expression          -->     SimpleExpression [ Relation SimpleExpression ]
SimpleExpression    -->     [UnaryOperator] Term {AddOperator Term}
Term                -->     Factor {MultOperator Factor}
Factor              -->     ynumber
                          | ystring | ynil
                          | Designator
                          | '(' Expression ')'
                          | ynot Factor
                          | FunctionCall
FunctionCall        -->     yident ActualParameters
Element             -->     ConstExpression ['..' ConstExpression ]


SubprogDeclList     -->     {ProcedureDecl ';' | FunctionDecl ';'}
ProcedureDecl       -->     ProcedureHeading ';' Block
FunctionDecl        -->     FunctionHeading ':' yident ';' Block
ProcedureHeading    -->     yprocedure yident [FormalParameters]
FunctionHeading     -->     yfunction yident [FormalParameters]
FormalParameters    -->     '(' OneFormalParam {';' OneFormalParam} ')'
OneFormalParam      -->     [yvar] IdentList ':' yident